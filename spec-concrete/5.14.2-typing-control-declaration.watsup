;;
;; Control local declaration typing
;;
;; syntax controlLocalDeclaration
;;

;;; constantDeclaration

rule ControlLocalDecl_ok/constantDeclaration:
  TC_0 |- constantDeclaration
        : TC_1 constantDeclarationIR
  -- ConstantDecl_ok:
      BLOCK TC_0 |- constantDeclaration
                  : TC_1 constantDeclarationIR

;;; instantiation

rule ControlLocalDecl_ok/instantiation:
  TC_0 |- instantiation
        : TC_1 instantiationIR
  -- InstanceDecl_ok:
      BLOCK TC_0 |- instantiation
                  : TC_1 instantiationIR

;;; variableDeclaration

rule ControlLocalDecl_ok/variableDeclaration:
  TC_0 |- variableDeclaration
        : TC_1 variableDeclarationIR
  -- VariableDecl_ok:
      BLOCK TC_0 |- variableDeclaration
                  : TC_1 variableDeclarationIR

;;; actionDeclaration

rule ControlLocalDecl_ok/actionDeclaration:
  TC_0 |- actionDeclaration
        : TC_1 actionDeclarationIR
  -- ActionDecl_ok:
      BLOCK TC_0 |- actionDeclaration
                  : TC_1 actionDeclarationIR

;;; tableDeclaration
;;; syntax tableDeclaration = TableD name tableProperty*

rule ControlLocalDecl_ok/tableDeclaration:
  TC_0 |- TableD name tableProperty*
       : TC_3 tableDeclarationIR
  ---- ;; set local context
  -- if TC_1 = TC_0 [ .LOCAL.KIND = TABLEAPPLYMETHOD ]
  ---- ;; check table properties
  -- Table_ok: TC_1 |- tableProperty* : TBLC tablePropertyIR*
  ---- ;; update the context with synthesized table
  -- TableType_ok: TC_1 TBLC |- name : TC_2 typeIR_table
  -- if TC_3 = $add_var(BLOCK, TC_2, name, NO typeIR_table DYN eps)
  ---- ;; create IR
  -- if tableDeclarationIR
      = TableD typeIR_table name tablePropertyIR*

;;
;; Control local declaration list typing
;;
;; syntax controlLocalDeclarationList
;;

rule ControlLocalDecls_ok/nil:
  TC |- eps : TC eps

rule ControlLocalDecls_ok/cons:
  TC_0 |- controlLocalDeclaration_h :: controlLocalDeclaration_t*
        : TC_2 (controlLocalDeclarationIR_h :: controlLocalDeclarationIR_t*)
  -- ControlLocalDecl_ok: TC_0 |- controlLocalDeclaration_h
                                : TC_1 controlLocalDeclarationIR_h
  -- ControlLocalDecls_ok: TC_1 |- controlLocalDeclaration_t*
                                 : TC_2 controlLocalDeclarationIR_t*
